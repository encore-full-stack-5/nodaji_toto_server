plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.0'
	id 'io.spring.dependency-management' version '1.1.5'

	id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	runtimeOnly 'com.mysql:mysql-connector-j'

	implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.5'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testRuntimeOnly 'com.h2database:h2'
}

jacocoTestCoverageVerification{
	violationRules {
		enabled = true
		rule {
			element = 'CLASS'
			limit {
				counter = 'LINE'
				value = 'TOTALCOUNT'
				maximum = 200
			}
		}
		rule {
			element = 'CLASS'
			includes = [
					'com.example.toto.controller.BettingController',
					'com.example.toto.controller.GameController',
					'com.example.toto.service.**'
			]
			limit {
				value = 'COVEREDRATIO'
				minimum = 0.8
			}
		}
	}
}

jacocoTestReport {
	dependsOn test
	reports{
		html.required = true
	}
	finalizedBy('jacocoTestCoverageVerification')
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy('jacocoTestReport')
}
